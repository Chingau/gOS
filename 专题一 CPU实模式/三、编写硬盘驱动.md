（该文章对应代码的 sha:28d812d7）

在前一篇文章《二、突破512字节》中是利用 int 13 编写的软盘驱动，把我们写的 setup.asm 烧录到软件的 0盘0道2扇区的位置，然后 int 13 读取到内存的 0x500 处执行。

由于软盘已经退出历史的舞台，我们也没必要在去学习软盘的相关知识了，所以本篇编写硬盘驱动，利用硬盘启动程序。

原理还是一样的，我们把 setup.asm 编译后的代码提前烧录到硬盘的 0盘0道2扇区处，然后在 MBR 中编译硬盘驱动程序去读取硬盘的 LBA=2 的位置(实际编写的硬盘驱动中，我们使用的都是 LBA模式，而不是 CHS模式)。

硬盘驱动本质上就是操作硬盘的那几个寄存器（0x1f0 ~ 0x1f7)。关于硬盘的相关原理介绍请参考文档《机械硬盘介绍.md》。

下面贴出 MBR 的程序，该程序的主要功能就是从硬盘的 LBA=2 处开始读取数据，然后把读出的数据存入放到内存地址 0x500 处，读取的大小取决于 setup.asm 的大小(本示例中setup.asm未超过512B，所以只用读取1个扇区即可)。

```c
;0柱面0磁道1扇区
[ORG  0x7c00]

[SECTION .data]
SETUP_MAIN_ADDR equ 0x500                   ;内核加载器在内存中的位置

[SECTION .text]
[BITS 16]
global _start
_start:
    ;设置屏幕模式为文本模式，清除屏幕
    mov ax, 3
    int 0x10

    mov dx, 0x1f2       ;指定读取或定入的扇区数
    mov al, 1           ;读取1个扇区, 根据setup.asm文件的大小实际设置
    out dx, al          ;操作硬盘寄存器

    mov ecx, 1          ;从硬盘的第1扇区开始读
    ;设置LBA的地址
    mov dx, 0x1f3
    mov al, cl
    out dx, al          ;LBA地址的低8位

    mov dx, 0x1f4
    mov al, ch
    out dx, al          ;LBA地址的中8位

    mov dx, 0x1f5
    shr ecx, 16
    mov al, cl
    out dx, al          ;LBA地址的高8位

    ;bit0~3: LBA地址的24~27位
    ;bit4: 0-主盘 1-从盘
    ;bit5, bit7: 固定为1
    ;bit6: 1-LBA模式 0-CHS模式
    xchg bx, bx
    mov dx, 0x1f6
    mov al, ch
    and al, 0b00001111
    or al, 0b11100000       ;主盘，LBA模式
    out dx, al

    ;命令或状态端口
    mov dx, 0x1f7
    mov al, 0x20        ;读命令，即以读的方式操作硬盘
    out dx, al          ;断点

    ;验证状态
    ;bit3: 0-表示硬盘未准备好 1-准备好了
    ;bit7: 0-表示硬盘不忙 1-表示硬盘忙
    ;bit0: 0-表示前一条指令正常执行 1-表示执行出错，出错信息通过0x1f1端口获得
.read_check:
    mov dx, 0x1f7
    in al, dx
    and al, 0b10001000      ;取硬盘状态的bit3,bit7
    cmp al, 0b00001000      ;硬盘数据已准备好且不忙
    jnz .read_check
    ;读数据
    mov dx, 0x1f0
    mov cx, 256
    mov edi, SETUP_MAIN_ADDR
.read_data:
    in ax, dx
    mov [edi], ax
    add edi, 2
    loop .read_data

    mov si, jmp_to_setup
    call print
    xchg bx, bx

    jmp SETUP_MAIN_ADDR         ;移交控制权

    ;正常情况下不会执行到下面3行代码
    mov si, read_floppy_error
    call print
    jmp $

; 如何调用
; mov     si, msg   ; 1 传入字符串
; call    print     ; 2 调用
; 函数原型：print(register si)
print:
    mov ah, 0x0e
    mov bh, 0
    mov bl, 0x01
.loop:
    mov al, [si]
    cmp al, 0
    jz .done
    int 0x10
    inc si
    jmp .loop
.done:
    ret

read_floppy_error:
    db "read disk error!", 10, 13, 0      ;read disk error!\r\n0

jmp_to_setup:
    db "jump to setup...", 10, 13, 0        ;jump to setup...\r\n0

;clear zero
times 510 - ($ - $$) db 0
dw 0xaa55
```

setup.asm 中仅实现一个打印字符串的功能以保证调试。setup.asm编译后需要先烧录到硬盘的第2扇区起始处，烧写方式是使用linux的 dd 命令，具体方法请参考《搭建操作系统框架.md》文中关于 dd 的介绍。

另外注意一点：从硬盘的哪个扇区开始读 setup.asm，如果我们是用 LBA 模式的话，那应该从硬盘的第 1 个扇区开始读(因为逻辑扇区从0开始计，第0个扇区存放的是 MBR)；如果我们是用 CHS 模式的话，那应该从硬盘的第 2 个扇区开始读(因为CHS扇区是从1开始计算，第1个扇区存放的是MBR)

