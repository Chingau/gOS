我们知道，CPU上电后，执行BIOS程序，BIOS程序把 MBR 搬移到内存地址 0x7c00 处，然后把控制权交给 MBR，但MBR 所在的 0盘0道1扇区为512字节，太小了，显然不能干太多事，所以MBR的主要工作就是把存储在硬盘(本节我们用的是软盘)中的内核加载器搬移到内存中，那内核加载器搬移到那里勒？本节我们实现的是把内核加载器搬移到内存地址 0x500 处，原因是实模式下的1MB内存空间的 0x500~0x7BFF 这段内存是程序员可支配的。

具体的实现步骤为：

1. 我们写的内核加载器程序需要保存在软盘的 0盘0道起始扇区为2扇区中 （因为0盘0道1扇区保存的是 MBR 程序），当然内核加载器的程序是可以大到占多个扇区的，只不过保存的起始位置为 0盘0道2扇区。
2. 我们在 MBR 程序中要实现这样的代码：搬移起始地址为 0盘0道2扇区 中的代码到内存地址 0x500 处。
3. 转交CPU控制权给内核加载器(即跳转到 0x500 处执行代码)。

经过以上3个步骤，CPU现在运行的就是我们自己实现的内核加载器程序了，同时，也突破了512字节的魔咒。

MBR 程序实现如下：

```c
;0柱面0磁道1扇区
[ORG  0x7c00]

[SECTION .data]
BOOT_MAIN_ADDR equ 0x500

[SECTION .text]
[BITS 16]
global _start
_start:
    ; 设置屏幕模式为文本模式，清除屏幕
    mov ax, 3
    int 0x10

    ; 将内核加载器读入0x500处
    ; 读盘
    mov     ch, 0                       ; 0 柱面
    mov     dh, 0                       ; 0 磁头
    mov     cl, 2                       ; 2 扇区，这是我们的内核加载器保存的起始位置
    mov     bx, BOOT_MAIN_ADDR          ; 数据往哪读

    mov     ah, 0x02                    ; 读盘操作
    mov     al, 1                       ; 连续读几个扇区
    mov     dl, 0                       ; 驱动器编号
    int     0x13

    
    mov     si, jmp_to_setup            ; 打印一些必要的提示信息
    call    print

    xchg bx, bx                         ; 调试用，相当于 bochs 的断点

    jmp     BOOT_MAIN_ADDR              ; 控制权移交

read_floppy_error:
    mov     si, read_floppy_error_msg
    call    print

    jmp $

; 如何调用
; mov     si, msg   ; 1 传入字符串
; call    print     ; 2 调用
print:
    mov ah, 0x0e
    mov bh, 0
    mov bl, 0x01
.loop:
    mov al, [si]
    cmp al, 0
    jz .done
    int 0x10

    inc si
    jmp .loop
.done:
    ret

read_floppy_error_msg:
    db "read floppy error!", 10, 13, 0  ; "read floppy error!"\r\n0

jmp_to_setup:
    db "jump to setup...", 10, 13, 0    ; "jump to setup..."\r\n0

times 510 - ($ - $$) db 0
db 0x55, 0xaa
```

提个问题：

上述代码中 read_floppy_error_msg，jmp_to_setup 定义的是数据，为什么要放在代码段中？可不可以存放在 data 段中，如下：

```
[SECTION .data]
BOOT_MAIN_ADDR equ 0x500

read_floppy_error_msg:
    db "read floppy error!", 10, 13, 0

jmp_to_setup:
    db "jump to setup...", 10, 13, 0
```

答案是不可以，原因请参数 《nasm.md》文档中的 “NASM \$,\$\$和vstart,org” 这一节的 "vstart, org" 这一小节。


然后我们的内核加载器对应的文件就是 setup.asm，用于测试，该文件只实现了一个打印功能，如下：

```
; 0柱面0磁道2扇区
[ORG 0x500]

[SECTION .data]
msg:
    db "hello", 10, 13, 0

[SECTION .text]
[BITS 16]
global _start
_start:
    mov     ax, 0
    mov     ss, ax
    mov     ds, ax
    mov     es, ax
    mov     fs, ax
    mov     gs, ax
    mov     si, ax

    mov     si, msg
    call    print

    jmp     $

; 如何调用
; mov     si, msg   ; 1 传入字符串
; call    print     ; 2 调用
print:
    mov ah, 0x0e
    mov bh, 0
    mov bl, 0x01
.loop:
    mov al, [si]
    cmp al, 0
    jz .done
    int 0x10

    inc si
    jmp .loop
.done:
    ret
```

我们编译后得到的 setup.o 需要我们自己烧入到软盘的 0盘0道2扇区的位置。